plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
}

def COMPATIBLE_VERSIONS = ['1.21', '1.22']

tasks.register("printEnv") {
    doLast {
        def envFile = new File(System.getenv("GITHUB_ENV"))
        envFile.append("MOD_VERSION=${project.mod_version}\n")
        envFile.append("RELEASE_NAME=${project.archives_base_name}-${project.mod_version}\n")
        envFile.append("GAME_VERSIONS=${COMPATIBLE_VERSIONS}\n")
    }
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        url = uri("https://maven.wispforest.io/releases")
    }
    maven {
        name = 'Repsy Anvian'
        url = 'https://repo.repsy.io/mvn/anvian/anvians-lib'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modLocalRuntime("com.terraformersmc:modmenu:${project.modmenu_version}")

    annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_version}")
    include "io.wispforest:owo-sentinel:${project.owo_version}"

    modImplementation "net.anvian.anvianslib:anvianslib-fabric-1.21:${project.anvianslib_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}
